plugins {
    id 'java'

        id 'com.gradleup.shadow' version '9.3.1'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()

    maven {
        name = "Hytale Releases"
        url = "https://maven.hytale.com/release"
    }

    maven {
        name = "Hytale Pre-Releases"
        url = "https://maven.hytale.com/pre-release"
    }
}

dependencies {
    compileOnly("com.hypixel.hytale:Server:2026.01.22-6f8bdbdc4")

    implementation("org.luaj:luaj-jse:3.0.1")
    implementation("net.dv8tion:JDA:$jda_version") {
        exclude module: 'opus-java'
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "name", project.mod_name
    inputs.property "description", project.mod_description
    inputs.property "hytale_version", project.hytale_version

    filteringCharset "UTF-8"

    filesMatching("manifest.json") {
        expand "version": project.version.toString(),
                "name": project.mod_name,
                "description": project.mod_description,
                "hytale_version": project.hytale_version
    }
}

shadowJar {
    archiveClassifier = ''

    relocate 'net.dv8tion.jda', 'me.theclashfruit.shadow.jda'
    relocate 'org.luaj', 'me.theclashfruit.shadow.luaj'
}

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

compileJava {
    options.encoding = "UTF-8"
    options.release = 25
}