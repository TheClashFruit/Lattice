plugins {
    id 'java'

    id 'com.gradleup.shadow' version '9.3.1'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
}

dependencies {
    // Hytale Server (API)
    compileOnly(files("../2026.01.13-50e69c385/Server/HytaleServer.jar"))

    implementation "com.discord4j:discord4j-core:$discord4j_version"
}

processResources {
    inputs.property "version", project.version
    inputs.property "name", project.mod_name
    inputs.property "description", project.mod_description
    inputs.property "hytale_version", project.hytale_version

    filteringCharset "UTF-8"

    filesMatching("manifest.json") {
        expand "version": project.version.toString(),
                "name": project.mod_name,
                "description": project.mod_description,
                "hytale_version": project.hytale_version
    }
}

shadowJar {
    relocate 'com.discord4j', 'me.theclashfruit.shadow.com.discord4j'
    relocate 'discord4j', 'me.theclashfruit.shadow.discord4j'
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

    // withSourcesJar()
}